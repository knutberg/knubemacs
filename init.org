#+PROPERTY: header-args :tangle yes :results silent
My personal emacs configuration made for macos running on a macbook. It doesn't
have any version or os checks, so you should probably adapt this for your own
setup if you want to use it.


* startup and quality of life
Section for basic emacs settings and various "quality of life"-stuff.

** early-init.el
This subsection tangles =~/.emacs.d/early-init.el= which is loaded before
=init.el= or any package or UI initialization. It's not a *big deal*, but it
improves startup a bit.

=file-name-handler-alist= is set to nil and reset after init. The
variable file-name-handler-alist holds a list of handlers, together
with regular expressions that determine when to apply each handler. In
short, we don't need that during init.

Bumping =gc-cons-threshold= and =gc-cons-percentage= for a faster
init. Both will be set to more reasonable values later in =init.org=.

We also invoke the [[https://www.gnu.org/software/emacs/manual/html_node/elisp/Error-Debugging.html][debugger]] for startup.

#+begin_src emacs-lisp :tangle early-init.el
;;; early-init.el --- -*- no-byte-compile: t-*-
(defvar startup-file-name-handler-alist file-name-handler-alist)
(setq file-name-handler-alist nil)                   ; reset later

(setq gc-cons-threshold         most-positive-fixnum ; set to 32MB later
      gc-cons-percentage        0.6                  ; set to 0.1 later
      debug-on-error            t                    ; reset to nil later
      site-run-file             nil                  ; disable site-start.el
      package-enable-at-startup nil)                 ; we use straight.el

(menu-bar-mode   -1)
(tool-bar-mode   -1)
(scroll-bar-mode -1)

(unless (and (display-graphic-p) (eq system-type 'darwin))
  (push '(menu-bar-lines . 0) default-frame-alist))
(push '(tool-bar-lines . 0) default-frame-alist)
(push '(vertical-scroll-bars) default-frame-alist)

(setq inhibit-startup-message t
      inhibit-scratch-message t
      load-prefer-newer       t)

(when (file-exists-p "~/.emacs.d/straight/repos/auto-compile/")
  (progn
    (add-to-list 'load-path "~/.emacs.d/straight/repos/auto-compile/")
    (add-to-list 'load-path "~/.emacs.d/straight/repos/packed/")
    (require 'auto-compile)
    (auto-compile-on-load-mode +1)
    (auto-compile-on-save-mode +1)))

(when (file-exists-p "~/.emacs.d/straight/repos/no-littering/")
  (progn
    (add-to-list  'load-path "~/.emacs.d/straight/repos/no-littering/")
    (require 'no-littering)
    (setq auto-save-file-name-transforms
          `((".*" ,(no-littering-expand-var-file-name "auto-save/") t)))
    (require 'recentf)
    (add-to-list 'recentf-exclude no-littering-var-directory)
    (add-to-list 'recentf-exclude no-littering-etc-directory)))
#+end_src

** custom.el
Save custom settings to =~/.emacs.d/custom.el=.
#+begin_src emacs-lisp
;;; init.el --- -*- lexical-binding: t -*-
(setq custom-file (concat user-emacs-directory "custom.el"))
(load custom-file 'noerror)
#+end_src

** user info
We load user info from =~/.emacs.d/user-info.el= which contains
#+begin_src emacs-lisp :tangle no
(setq user-full-name         "name"
      user-mail-address      "mail"
      calendar-latitude      0
      calendar-longitude     0
      calendar-location-name "location")
#+end_src

#+begin_src emacs-lisp
(setq user-info-file (concat user-emacs-directory "user-info.el"))
(when (file-exists-p user-info-file)
  (load user-info-file 'noerror))
#+end_src

** remove built-in org
We will later install the most recent (stable) version of org.
#+begin_src emacs-lisp
(require 'cl-seq)
(setq load-path
      (cl-remove-if
       (lambda (x)
         (string-match-p "org$" x))
       load-path))
#+end_src

** package management
We use [[https://github.com/raxod502/straight.el/tree/develop][straight.el]] to install packages. [[https://github.com/emacscollective/auto-compile][auto-compile]] is loaded in =early-init.el= as
possible to ensure we don't load outdated byte code files.
#+begin_src emacs-lisp
(defvar bootstrap-version)
(let ((bootstrap-file
       (expand-file-name "straight/repos/straight.el/bootstrap.el" user-emacs-directory))
      (bootstrap-version 5))
  (unless (file-exists-p bootstrap-file)
    (with-current-buffer
        (url-retrieve-synchronously
         "https://raw.githubusercontent.com/raxod502/straight.el/develop/install.el"
         'silent 'inhibit-cookies)
      (goto-char (point-max))
      (eval-print-last-sexp)))
  (load bootstrap-file nil 'nomessage))

(straight-use-package 'auto-compile)
#+end_src

** keybindings
*** general.el
[[https://github.com/noctuid/general.el][general.el]] is a decent way to configure keybindings, particularly when it comes
to various mode maps and evil.
#+begin_src emacs-lisp
(straight-use-package 'general)
#+end_src
*** macos
We set command to meta. Option is unbound due to various special character
inputs. macos shortcut(s) that use command are moved to option (System
Preferences > Keyboard > Shortcuts) or are simply disabled.
#+begin_src emacs-lisp
(setq mac-command-modifier      'meta
      mac-option-modifier       nil
      mac-right-option-modifier nil
      mac-function-modifier     nil)
#+end_src

*** initial unbinds
Default emacs comes with a bazillion annoying and useless keybindings. Expect
this section to grow!
#+begin_src emacs-lisp
(general-unbind
  "s-p"      ; no one needs print
  "C-x f"    ; set-fill-column is always 80
  "C-x C-n")  ; set-goal-column is just annoying
#+end_src

*** which-key
Display keybidings with [[https://github.com/justbur/emacs-which-key][which-key]].
#+begin_src emacs-lisp
(straight-use-package 'which-key)

(setq which-key-idle-delay    0.8
      which-key-separator     " "
      which-key-sort-order    'which-key-description-order
      which-key-prefix-prefix "+")

(which-key-mode +1)
#+end_src

** garbage collection
We set =gc= variables to more reasonable values at the end of =emacs-startup=.
While we're at it we also reset =debug= and =file-name-handler-alist= from
=early-init.el=
#+begin_src emacs-lisp
;; Increase this if stuttering occurs. Decrease if freezes occurs.
(defvar knube-gc-cons-threshold (* 64 1024 1024))

(add-hook 'emacs-startup-hook
          (lambda ()
            (setq gc-cons-threshold       knube-gc-cons-threshold
                  gc-cons-percentage      0.1
                  debug-on-error          nil
                  file-name-handler-alist startup-file-name-handler-alist)
            (makunbound 'startup-file-name-handler-alist)))

;; Do gc when out of focus. Avoid gc when using minibuffer.
(add-hook 'emacs-startup-hook
          (lambda ()
            (if (boundp 'after-focus-change-function)
                (add-function :after after-focus-change-function
                              (lambda ()
                                (unless (frame-focus-state)
                                  (garbage-collect))))
              (add-hook 'after-focus-change-function 'garbage-collect))
            (defun gc-minibuffer-setup-hook ()
              (setq gc-cons-threshold (* knube-gc-cons-threshold 2)))

            (defun gc-minibuffer-exit-hook ()
              (garbage-collect)
              (setq gc-cons-threshold knube-gc-cons-threshold))

            (add-hook 'minibuffer-setup-hook #'gc-minibuffer-setup-hook)
            (add-hook 'minibuffer-exit-hook #'gc-minibuffer-exit-hook)))
#+end_src

** env and path variables
env and path variables are almost always annoying, specially in macos. in macos. Steve Purcell's [[https://github.com/purcell/exec-path-from-shell][exec-path-from-shell]] makes all of that a bit easier.
#+begin_src emacs-lisp
(straight-use-package 'exec-path-from-shell)

(exec-path-from-shell-initialize)

(exec-path-from-shell-copy-envs '("LANG"
                                  "LC_ALL"
                                  "PYTHONPATH"))
#+end_src

** no-littering
Keep =~/.emacs.d/= clean with [[https://github.com/emacscollective/no-littering][no-littering]]. Loaded in =early-init.el=.
#+begin_src emacs-lisp
(straight-use-package 'no-littering)
#+end_src

** crux and various keybinds
A [[https://github.com/bbatsov/crux][*c*ollection of *r*idiculously *u*seful e*x*tensions for emacs]].
#+begin_src emacs-lisp
  (straight-use-package 'crux)

  (general-define-key
   "C-c o"   'crux-open-with
   "C-k"     'crux-smart-kill-line
   "C-S-RET" 'crux-smart-open-line-above
   "S-RET"   'crux-smart-open-line
   "C-c n"   'crux-cleanup-buffer-or-region
   "C-c f"   'crux-recentf-find-file
   "C-c F"   'crux-recentf-find-directory
   "C-c e"   'crux-eval-and-replace
   "C-c D"   'crux-delete-file-and-buffer
   "M-/"     'hippie-expand
   "C-x C-b" 'ibuffer
   "M-z"     'zap-up-to-char
   "C-s"     'isearch-forward-regexp
   "C-r"     'isearch-backward-regexp
   "C-M-s"   'isearch-forward
   "C-M-r"   'isearch-backward
   "M-;"     'knube/comment-or-uncomment)

  (autoload 'zap-up-to-char "misc"
    "Kill up to, but not including ARGth occurrence of CHAR." t)

  (defun knube/comment-or-uncomment ()
    (interactive)
    (let (beg end)
      (if (region-active-p)
          (setq beg (region-beginning) end (region-end))
        (setq beg (line-beginning-position) end (line-end-position)))
      (comment-or-uncomment-region beg end)))
#+end_src

** smartparens
Automatic symbol pairing with https://github.com/Fuco1/smartparens. Currently
global, but we might tweak this later if it becomes annoying.
#+begin_src emacs-lisp
(straight-use-package 'smartparens)

(require 'smartparens-config)

(smartparens-global-mode +1)
#+end_src

** utf-8
One locale to rule them all.
#+begin_src emacs-lisp
(setq utf-translate-cjk-mode nil     ; disable CJK coding/encoding
      locale-coding-system   'utf-8)
(set-language-environment    'utf-8)
(set-default-coding-systems  'utf-8)
(set-terminal-coding-system  'utf-8)
(set-selection-coding-system 'utf-8)
(prefer-coding-system        'utf-8)
#+end_src

** mouse scrolling
This makes scrolling a bit better with the macbook touchpad.
#+begin_src emacs-lisp
(setq scroll-step                     1
      scroll-conservatively           101
      scroll-preserve-screen-position 'always
      next-screen-context-lines       5
      debugger-stack-frame-as-list    t
      mouse-wheel-follow-mouse        t
      mouse-wheel-scroll-amount       '(1 ((shift) . 1))
      mouse-wheel-progressive-speed   nil
      mouse-yank-at-point             t)
#+end_src

** start maximized
Start emacs with a maximized frame.
#+begin_src emacs-lisp
(toggle-frame-maximized)
#+end_src

** various unsorted stuff
Most of this is copied from other emacs configs.
#+begin_src emacs-lisp
(add-hook 'prog-mode-hook   'subword-mode)
(add-hook 'before-save-hook 'delete-trailing-whitespace)
(add-hook 'before-save-hook
          (lambda ()
            (when buffer-file-name
              (let ((dir (file-name-directory buffer-file-name)))
                (when (and (not (file-exists-p dir))
                           (y-or-n-p (format "Directory %s does not exist. Create it?" dir)))
                  (make-directory dir t))))))

(blink-cursor-mode       0)
(delete-selection-mode   1)
(transient-mark-mode     1) ; https://www.emacswiki.org/emacs/TransientMarkMode
(save-place-mode         1) ; https://www.emacswiki.org/emacs/SavePlace
;;(save-hist-mode          1)
(show-paren-mode         1) ; Indicate matching pairs of parentheses
(column-number-mode      1)
(global-font-lock-mode   t) ; is this really a good idea?
(global-auto-revert-mode t) ; refresh buffer on file change

(setq-default cursor-type           'bar
              indent-tabs-mode       nil  ; indent with space
              fill-column            80   ; always break at 80
              abbrev-mode            t
              dired-listing-switches "-alh")

(require 'uniquify)
(setq uniquify-buffer-name-style          'forward ; unique buffer names
      show-paren-delay                    0.0
      tab-width                           2
      delete-selection-mode               t
      sentence-end-double-space           nil
      vc-follow-symlinks                  t
      default-directory                   "~/"
      confirm-kill-emacs                  'y-or-n-p
      require-final-newline               t
      visible-bell                        t
      save-interprogram-paste-before-kill t
      apropos-do-all                      t
      save-abbrevs                        'silently
      large-file-warning-threshold        (* 15 1024 1024)
      global-mark-ring-max                500  ; we have buttloads of
      mark-ring-max                       500  ; memory, might as well
      kill-ring-max                       500) ; use it

(fset 'yes-or-no-p 'y-or-n-p)

(setq backup-directory-alist `((".*" . ,temporary-file-directory)))
(setq auto-save-file-name-transforms `((".*" ,temporary-file-directory t)))
#+end_src

* ui
Everything remotely "UI"-related goes here.
** fonts
Monospaced fonts makes life easier. Currently, my favorite is [[https://github.com/tonsky/FiraCode][Fira Code]].
#+begin_src emacs-lisp
(set-face-attribute 'default nil
                    :family "Fira Code"
                    :height 180
                    :weight 'light)
(set-face-attribute 'fixed-pitch nil
                    :family "Fira Code"
                    :height 180
                    :weight 'light)
(set-face-attribute 'variable-pitch nil
                    :family "Fira Code"
                    :height 180
                    :weight 'light)
#+end_src

emacs 27 introduced the new =:extend= face attribute. In turn, this makes my
org-blocks look weird when switching themes. This fixes it?
#+begin_src emacs-lisp
(defun knube/fix-org-blocks ()
  (interactive)
  (eval-after-load 'org
    (lambda ()
      (set-face-attribute 'org-block            nil
                          :extend               t)
      (set-face-attribute 'org-block-begin-line nil
                          :extend               t
                          :underline            nil
                          :overline             t
                          :slant                'italic)
      (set-face-attribute 'org-block-end-line   nil
                          :extend               t
                          :underline            t
                          :overline             nil
                          :slant                'italic))))
#+end_src

** theme
[[https://github.com/belak/base16-emacs][base16 themes]] work just fine. I manually switch to =base16-default-light= when
in bright light conditions.
#+begin_src emacs-lisp
(straight-use-package 'base16-theme)

(load-theme 'base16-default-dark t)

(knube/fix-org-blocks)
#+end_src

** modeline
*** minions
[[https://github.com/tarsius/minions][minions]] packs all minor modes into one little icon.
#+begin_src emacs-lisp
(straight-use-package 'minions)

(setq minions-mode-line-lighter    "☰"
      minions-mode-line-delimiters '("" . ""))

(minions-mode +1)
#+end_src

*** telephone-line
[[https://github.com/dbordak/telephone-line][telephone-line]] looks good
#+begin_src emacs-lisp
(straight-use-package 'telephone-line)
(setq telephone-line-lhs
      '((evil   . (telephone-line-evil-tag-segment
                   telephone-line-airline-position-segment))
        (accent . (telephone-line-buffer-name-segment))
        (nil    . (telephone-line-buffer-modified-segment)))

      telephone-line-rhs
      '((nil    . (telephone-line-minions-mode-segment))
        (accent . (telephone-line-vc-segment))
        (nil    . (telephone-line-misc-info-segment))))

(unless (equal "Battery status not available"
               (battery))
  (display-battery-mode +1))

(setq display-time-24hr-format            t
      display-time-day-and-date           t
      display-time-default-load-average   nil
      display-time-load-average           nil
      display-time-load-average-threshold nil)
(display-time-mode +1)

(telephone-line-mode +1)
#+end_src

** writeroom
I use [[https://github.com/joostkremers/writeroom-mode][writeroom-mode]] for an uncluttered and minimalistic writing experience.
#+begin_src emacs-lisp
(straight-use-package 'writeroom-mode)

(general-define-key
 "<f6>" 'writeroom-mode)
#+end_src

* evil
* completion
** emacs ui completion
*** selectrum
[[https://github.com/raxod502/selectrum][selectrum]] for incremental narrowing in emacs.
#+begin_src emacs-lisp
(straight-use-package 'selectrum)
(straight-use-package 'selectrum-prescient)

(general-define-key
 "C-x C-z" 'selectrum-repeat)

(selectrum-mode +1)
(selectrum-prescient-mode +1)
(prescient-persist-mode +1)
#+end_src

*** consult
[[https://github.com/minad/consult][consult]] builds on emacs' [[https://www.gnu.org/software/emacs/manual/html_node/elisp/Minibuffer-Completion.html][completing-read]]. Works well with selectrum, marginalia and embark.
#+begin_src emacs-lisp
(straight-use-package 'consult)

(general-define-key
 "C-c h" 'consult-history
 "C-c m" 'consult-mode-command
 "C-c b" 'consult-bookmark
 "C-c k" 'consult-kmacro

 ;; C-x bindings (ctrl-x-map)
 "C-x M-:" 'consult-complex-command     ; repeat-complex-command
 "C-x b"   'consult-buffer              ; switch-to-buffer
 "C-x 4 b" 'consult-buffer-other-window ; switch-to-buffer-other-window
 "C-x 5 b" 'consult-buffer-other-frame  ; switch-to-buffer-other-frame

 ;; Custom M-# bindings for fast register access
 "M-#"   'consult-register-load
 "M-'"   'consult-register-store ; abbrev-prefix-mark (unrelated)
 "C-M-#" 'consult-register

 ;; Other custom bindings
 "M-y"      'consult-yank-pop ; yank-pop
 "<help> a" 'consult-apropos  ; apropos-command

 ;; M-g bindings (goto-map)
 "M-g e"   'consult-compile-error
 "M-g f"   'consult-flymake   ; or consult-flycheck
 "M-g g"   'consult-goto-line ; goto-line
 "M-g M-g" 'consult-goto-line ; goto-line
 "M-g o"   'consult-outline
 "M-g m"   'consult-mark
 "M-g k"   'consult-global-mark
 "M-g i"   'consult-imenu
 "M-g I"   'consult-project-imenu

 ;; M-s bindings (search-map)
 "M-s f" 'consult-find
 "M-s L" 'consult-locate
 "M-s g" 'consult-grep
 "M-s G" 'consult-git-grep
 "M-s r" 'consult-ripgrep
 "M-s l" 'consult-line
 "M-s m" 'consult-multi-occur
 "M-s k" 'consult-keep-lines
 "M-s u" 'consult-focus-lines

 ;; Isearch integration
 "M-s e" 'consult-isearch)

(general-define-key
 :keymaps 'isearch-mode-map
 "M-e"   'consult-isearch ; isearch-edit-string
 "M-s e" 'consult-isearch ; isearch-edit-string
 "M-s l" 'consult-line)   ; required by consult-line to detect isearch
#+end_src

*** marginalia
[[https://github.com/minad/marginalia][marginalia]] adds annotations to minibuffer completions.
#+begin_src emacs-lisp
(straight-use-package 'marginalia)

(marginalia-mode +1)

(general-define-key
 :keymaps 'minibuffer-local-map
 "M-S-a" 'marginalia-cycle)
#+end_src

*** embark
[[https://github.com/oantolin/embark][embark]] provides a contextual menu through =embark-act=.
#+begin_src emacs-lisp
(straight-use-package 'embark)
(straight-use-package 'embark-consult)

(general-define-key
 "C-."   'embark-act
 "C-;"   'embark-dwim
 "C-h B" 'embark-bindings)

(setq prefix-help-command #'embark-prefix-help-command)

(add-hook 'embark-collect-mode-hook 'consult-preview-at-point-mode)
#+end_src

*** bibtex-actions
[[https://github.com/bdarcus/bibtex-actions][bibtex-actions]] uses the framework provided above for quick and easy bibtex citations.
#+begin_src emacs-lisp
(straight-use-package 'bibtex-actions)

(general-define-key
 "C-c b" 'bibtex-actions-insert-citation)

(general-define-key
 :keymaps 'minibuffer-local-map
 "M-b" 'bibtex-action-insert-preset)

(eval-after-load 'embark
  (lambda ()
    (add-to-list 'embark-keymap-alist '(bibtex . bibtex-actions-map))))

(setq bibtex-completion-bibliography "~/Dropbox/org/bibfiles/references.bib"
      bibtex-completion-additional-search-fields '(doi url)
      bibtex-actions-at-point-function           'embark-act
      org-cite-follow-processor                  'bibtex-actions
      org-cite-insert-processor                  'bibtex-actions)

(advice-add #'completing-read-multiple :override #'consult-completing-read-multiple)
#+end_src

** code completion
*** company
Code completion with /frecency/. [[http://company-mode.github.io/][company]] has several addons and backends, those
will be installed and configured in their respective sections later.

#+begin_src emacs-lisp
(straight-use-package 'company)
(straight-use-package 'company-prescient)

(global-company-mode    +1)
(company-prescient-mode +1)

(setq company-idle-delay                0.5
      company-show-numbers              t
      company-tooltip-limit             10
      company-minimum-prefix-length     2
      company-tooltip-align-annotations t
      ;; invert the navigation direction if the the completion
      ;; popup-isearch-match is displayed on top (happens near the bottom of
      ;; windows)
      company-tooltip-flip-when-above   t)
#+end_src

** yasnippet
[[https://github.com/joaotavora/yasnippet][yasnippet]] is great for providing bigger templates for your =.tex=- or =.org=-files.
#+begin_src emacs-lisp
(straight-use-package 'yasnippet)

(setq yas-snippet-dirs '("~/.emacs.d/snippets"))

(yas-global-mode +1)
#+end_src

* org-mode
org-mode is absolutely brilliant
** org
#+begin_src emacs-lisp
(straight-use-package 'org)
(straight-use-package 'org-contrib)

;; (require 'org-tempo)
;; (require 'ox-bibtex)
;; (require 'ob-latex)
;; (require 'ob-emacs-lisp)

(setq org-list-allow-alphabetical      t
      org-fontify-whole-heading-line   t
      org-startup-indented             nil  ; indent sections
      org-indent-indentation-per-level 0
      org-adapt-indentation            nil
      org-src-tab-acts-natively        t     ; tab works as in any major mode
      org-src-preserve-indentation     t
      org-log-into-drawer              t     ; wtf is this?
      org-src-fontify-natively         t     ; highlight code
      org-log-done                     'time ; add dates on completion of TODOs
      org-support-shift-select         t     ; select holding down shift
      org-startup-truncated            nil
      org-directory                    "~/Dropbox/org"
      org-agenda-files                 '("~/Dropbox/org/agenda")
      org-ellipsis                     " ⤵"
      org-src-window-setup             'current-window
      org-latex-pdf-process            (list "latexmk -xelatex -f %f"))

(add-hook 'org-mode-hook (lambda ()
                           (add-to-list 'org-structure-template-alist
                                        '("se" . "src emacs-lisp"))))

(org-babel-do-load-languages 'org-babel-load-languages
                             '((emacs-lisp . t)
                               (latex      . t)))

(general-unbind
  :keymaps 'org-mode-map
  "C-c '"  ; redefined below
  "C-c [") ; I have no need to "put whatever to the front of the agenda"
(general-define-key
 :keymaps 'org-mode-map
 "C-c C-'" 'org-edit-special)
(general-define-key
 :keymaps 'org-src-mode-map
 "C-c C-'" 'org-edit-src-exit)
#+end_src

** org-roam
TODO
** company-org-block
[[https://github.com/xenodium/company-org-block][company-org-block]] triggers with "<" and lets me quickly find the correct
org-block. ='auto= immediately triggers =org-edit-special=.
#+begin_src emacs-lisp
(straight-use-package 'company-org-block)

(setq company-org-block-edit-style 'auto) ;; 'auto, 'prompt, or 'inline

(add-hook 'org-mode-hook
          (lambda ()
            (add-to-list (make-local-variable 'company-backends)
                         'company-org-block)))
#+end_src

* latex
** auctex
#+begin_src emacs-lisp
(straight-use-package 'auctex)

(add-hook 'LaTeX-mode-hook 'reftex-mode)
(add-hook 'LaTeX-mode-hook 'LaTeX-math-mode)
(add-hook 'LaTeX-mode-hook 'TeX-PDF-mode)

(setq-default TeX-master nil
              TeX-engine 'xetex)

(setq TeX-source-correlate-method 'synctex
      TeX-source-correlate        t
      TeX-PDF-mode                t
      TeX-auto-save               t
      TeX-save-query              nil
      TeX-parse-self              t
      reftex-plug-into-AUCTeX     t
      TeX-view-program-list       '(("Skim" "/Applications/Skim.app/Contents/SharedSupport/displayline -g %n %o %b"))
      TeX-view-program-selection  '((output-pdf "Skim"))
      TeX-clean-confirm           nil)

;; make sure everything works fine with latexmk
(straight-use-package 'auctex-latexmk)

(setq auctex-latexmk-inherit-TeX-PDF-mode t)

(auctex-latexmk-setup)
#+end_src

** cdlatex
[[https://github.com/cdominik/cdlatex][cdlatex]] is quick and simple.
#+begin_src emacs-lisp
(straight-use-package 'cdlatex)

(add-hook 'org-mode-hook   'turn-on-org-cdlatex)
(add-hook 'LaTeX-mode-hook 'turn-on-cdlatex)

(setq cdlatex-env-alist
      '(("equation*" "\\begin{equation*}\n?\n\\end{equation*}\n" nil)))
#+end_src

** company-auctex
company completion for auctex. Do I need this?
#+begin_src emacs-lisp
;; (straight-use-package 'company-auctex)
;; (company-auctex-init)
#+end_src

** company-bibtex
Not sure?

* Local variables
# Local Variables:
# eval: (add-hook 'after-save-hook (lambda ()(org-babel-tangle)) nil t)
# End:
